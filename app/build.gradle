apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.oushangfeng.ounews"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        demo {
            storeFile file("../exercise.jks")
            storePassword "12345678"
            keyAlias "oubowu"
            keyPassword "12345678"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.demo
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.demo
        }
    }

    //修改生成的apk名字
    applicationVariants.all { variant ->

        if (variant.buildType.name.equals('release') || variant.buildType.name.equals('debug')) {
            variant.outputs.each { output ->
                def appName = 'OuNews'
                def oldFile = output.outputFile
                def buildName = ''

                variant.productFlavors.each { product ->
                    buildName = product.name
                }

                def releaseApkName = appName + '-' + defaultConfig.versionName + "-" + variant.buildType.name + '.apk'
                output.outputFile = new File(oldFile.parent, releaseApkName)
            }
        }
    }

    // 解决butterknife和lombok的冲突：Duplicate files copied in APK META-INF/services/javax.annotation.processing.Processor
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }

    productFlavors {
        all32 { minSdkVersion 12 }
        all64 { minSdkVersion 21 }
        armv5 { minSdkVersion 12 }
        armv7a { minSdkVersion 12 }
        arm64 { minSdkVersion 21 }
        x86 { minSdkVersion 12 }
        x86_64 { minSdkVersion 21 }
    }

}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

dependencies {
//    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'
    compile 'com.android.support:support-annotations:24.2.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.9'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // 使用okhttp3加载图片
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'de.greenrobot:greendao:2.1.0'
    compile 'zhou.widget:richtext:1.0.4'
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.github.zhaokaiqiang.klog:library:1.5.0'
    compile('com.afollestad.material-dialogs:core:0.9.0.1') {
        transitive = true
    }
    compile project(':changeskin')
    compile project(':ijkplayer')

    all32Compile project(':ijkplayer-armv5')
    all32Compile project(':ijkplayer-armv7a')
    all32Compile project(':ijkplayer-x86')
    all64Compile project(':ijkplayer-armv5')
    all64Compile project(':ijkplayer-armv7a')
    all64Compile project(':ijkplayer-arm64')
    all64Compile project(':ijkplayer-x86')
    all64Compile project(':ijkplayer-x86_64')
    armv5Compile project(':ijkplayer-armv5')
    armv7aCompile project(':ijkplayer-armv7a')
    arm64Compile project(':ijkplayer-arm64')
    x86Compile project(':ijkplayer-x86')
    x86_64Compile project(':ijkplayer-x86_64')
}